; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "DUnitTestInspector"
#define VersionNumber "0.3"
#define MyAppPublisher "DUnit project group"

[Setup]
AppName={#MyAppName}
AppVerName={#MyAppName} {#VersionNumber}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
OutputBaseFilename={#MyAppName}_setup
Compression=lzma
SolidCompression=yes

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Types]
Name: D2007; Description: Delphi 2007 installation
Name: D2009; Description: Delphi 2009 installation
Name: D2010; Description: Delphi 2010 installation

[Components]
Name: D2007; Description: Delphi 2007; Types: D2007
Name: D2009; Description: Delphi 2009; Types: D2009
Name: D2010; Description: Delphi 2010; Types: D2010

[Files]
Source: ..\_bin\DUnitTestInspector.bpl; DestDir: {app}; Flags: ignoreversion; AfterInstall: AddToDelphi2007KnownPackages; Components: D2007
Source: ..\_bin\OTAUtils.bpl; DestDir: {commondocs}\RAD Studio\5.0\Bpl; Flags: ignoreversion; Components: D2007

Source: ..\_bin\DUnitTestInspector.bpl; DestDir: {app}; Flags: ignoreversion; AfterInstall: AddToDelphi2009KnownPackages; Components: D2009
Source: ..\_bin\OTAUtils.bpl; DestDir: {commondocs}\RAD Studio\6.0\Bpl; Flags: ignoreversion; Components: D2009

Source: ..\_bin\DUnitTestInspector.bpl; DestDir: {app}; Flags: ignoreversion; AfterInstall: AddToDelphi2010KnownPackages; Components: D2010
Source: ..\_bin\OTAUtils.bpl; DestDir: {commondocs}\RAD Studio\7.0\Bpl; Flags: ignoreversion; Components: D2010
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKCU; Subkey: Software\DUnitTestInspector; ValueType: expandsz; ValueName: WatchFile; ValueData: {userappdata}\DUnitTestInspector\{#VersionNumber}\watchFile; Flags: uninsdeletekey; AfterInstall: CreateWatchFileFolder

[Icons]
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}

[Code]
procedure AddBDS5KnownPackageRegValue(APackageFilePath, APackageDescription: string);
begin
   RegWriteStringValue(HKEY_CURRENT_USER, 'Software\Borland\BDS\5.0\Known Packages',
    APackageFilePath, APackageDescription);
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\Borland\BDS\5.0\Disabled Packages',
    APackageFilePath)
end;

procedure DeleteBDS5KnownPackageRegValue(APackageFilePath: string);
begin
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\Borland\BDS\5.0\Known Packages',
    APackageFilePath)
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\Borland\BDS\5.0\Disabled Packages',
    APackageFilePath)
end;

procedure AddToDelphi2007KnownPackages;
begin
  AddBDS5KnownPackageRegValue(ExpandConstant('{app}\DUnitTestInspector.bpl'), 'DUnit Test Inspector')
end;

procedure DeleteFromDelphi2007KnownPackages;
begin
  DeleteBDS5KnownPackageRegValue(ExpandConstant('{app}\DUnitTestInspector.bpl'))
end;

procedure AddBDS6KnownPackageRegValue(APackageFilePath, APackageDescription: string);
begin
   RegWriteStringValue(HKEY_CURRENT_USER, 'Software\CodeGear\BDS\6.0\Known Packages',
    APackageFilePath, APackageDescription);
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\CodeGear\BDS\6.0\Disabled Packages',
    APackageFilePath)
end;

procedure DeleteBDS6KnownPackageRegValue(APackageFilePath: string);
begin
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\CodeGear\BDS\6.0\Known Packages',
    APackageFilePath)
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\CodeGear\BDS\6.0\Disabled Packages',
    APackageFilePath)
end;

procedure AddToDelphi2009KnownPackages;
begin
  AddBDS6KnownPackageRegValue(ExpandConstant('{app}\DUnitTestInspector.bpl'), 'DUnit Test Inspector')
end;

procedure DeleteFromDelphi2009KnownPackages;
begin
  DeleteBDS6KnownPackageRegValue(ExpandConstant('{app}\DUnitTestInspector.bpl'))
end;

procedure AddBDS7KnownPackageRegValue(APackageFilePath, APackageDescription: string);
begin
   RegWriteStringValue(HKEY_CURRENT_USER, 'Software\CodeGear\BDS\7.0\Known Packages',
    APackageFilePath, APackageDescription);
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\CodeGear\BDS\7.0\Disabled Packages',
    APackageFilePath)
end;

procedure DeleteBDS7KnownPackageRegValue(APackageFilePath: string);
begin
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\CodeGear\BDS\7.0\Known Packages',
    APackageFilePath)
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\CodeGear\BDS\7.0\Disabled Packages',
    APackageFilePath)
end;

procedure AddToDelphi2010KnownPackages;
begin
  AddBDS7KnownPackageRegValue(ExpandConstant('{app}\DUnitTestInspector.bpl'), 'DUnit Test Inspector')
end;

procedure DeleteFromDelphi2010KnownPackages;
begin
  DeleteBDS7KnownPackageRegValue(ExpandConstant('{app}\DUnitTestInspector.bpl'))
end;

procedure CreateWatchFileFolder();
begin
  ForceDirectories(ExpandConstant('{userappdata}\DUnitTestInspector\{#VersionNumber}'))
end;

procedure DeleteWatchFileFolder();
begin
  DelTree(ExpandConstant('{userappdata}\DUnitTestInspector'), true, true, true);
end;

procedure DeinitializeUninstall();
begin
  DeleteFromDelphi2007KnownPackages;
  DeleteFromDelphi2009KnownPackages;
  DeleteFromDelphi2010KnownPackages;
  DeleteWatchFileFolder;
end;
