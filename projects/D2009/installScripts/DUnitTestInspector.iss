; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define PluginName "DUnitTestInspector"
#define VersionNumber "0.3"
#define MyAppPublisher "DUnit project group"

#define PluginIDEDescription "DUnit Test Inspector"
#define PluginFileName "DUnitTestInspector.bpl"
#define OTAFileName "OTAUtils.bpl"
#define WatchFolder "{userappdata}\DUnitTestInspector"

#define D2007Folder "{commondocs}\RAD Studio\5.0\Bpl"
#define D2007PluginPath "{commondocs}\RAD Studio\5.0\Bpl\DUnitTestInspector.bpl"
#define D2007KnownPackageKey "Software\Borland\BDS\5.0\Known Packages"
#define D2007DisabledPackageKey "Software\Borland\BDS\5.0\Disabled Packages"

#define D2009Folder "{commondocs}\RAD Studio\6.0\Bpl"
#define D2009PluginPath "{commondocs}\RAD Studio\6.0\Bpl\DUnitTestInspector.bpl"
#define D2009KnownPackageKey "Software\CodeGear\BDS\6.0\Known Packages"
#define D2009DisabledPackageKey "Software\CodeGear\BDS\6.0\Disabled Packages"

#define D2010Folder "{commondocs}\RAD Studio\7.0\Bpl"
#define D2010PluginPath "{commondocs}\RAD Studio\7.0\Bpl\DUnitTestInspector.bpl"
#define D2010KnownPackageKey "Software\CodeGear\BDS\7.0\Known Packages"
#define D2010DisabledPackageKey "Software\CodeGear\BDS\7.0\Disabled Packages"

[Setup]
AppName={#PluginName}
AppVerName={#PluginName} {#VersionNumber}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\{#PluginName}
DefaultGroupName={#PluginName}
OutputBaseFilename={#PluginName}Setup
Compression=lzma
SolidCompression=yes

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Types]
Name: D2007; Description: Delphi 2007 installation
Name: D2009; Description: Delphi 2009 installation
Name: D2010; Description: Delphi 2010 installation

[Components]
Name: D2007; Description: Delphi 2007; Types: D2007
Name: D2009; Description: Delphi 2009; Types: D2009
Name: D2010; Description: Delphi 2010; Types: D2010

[Files]
Source: ..\_bin\{#PluginFileName}; DestDir: {#D2007Folder}; Flags: ignoreversion; Components: D2007
Source: ..\_bin\{#OTAFileName}; DestDir: {#D2007Folder}; Flags: ignoreversion; Components: D2007

Source: ..\_bin\{#PluginFileName}; DestDir: {#D2009Folder}; Flags: ignoreversion; Components: D2009
Source: ..\_bin\{#OTAFileName}; DestDir: {#D2009Folder}; Flags: ignoreversion; Components: D2009

Source: ..\_bin\{#PluginFileName}; DestDir: {#D2010Folder}; Flags: ignoreversion; Components: D2010
Source: ..\_bin\{#OTAFileName}; DestDir: {#D2010Folder}; Flags: ignoreversion; Components: D2010
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKCU; Subkey: Software\{#PluginName}; ValueType: expandsz; ValueName: WatchFile; ValueData: {#WatchFolder}\watchFile; Flags: uninsdeletekey; AfterInstall: CreateWatchFolder; Components: ; Languages: 

Root: HKCU; Subkey: {#D2007KnownPackageKey}; ValueType: expandsz; ValueName: {#D2007PluginPath}; ValueData: {#PluginIDEDescription}; Components: D2007; Languages: ; Flags: uninsdeletevalue
Root: HKCU; Subkey: {#D2007DisabledPackageKey}; ValueType: expandsz; ValueName: {#D2007PluginPath}; Flags: uninsdeletevalue deletevalue; Components: D2007

Root: HKCU; Subkey: {#D2009KnownPackageKey}; ValueType: expandsz; ValueName: {#D2009PluginPath}; ValueData: {#PluginIDEDescription}; Components: D2009; Languages: ; Flags: uninsdeletevalue
Root: HKCU; Subkey: {#D2009DisabledPackageKey}; ValueType: expandsz; ValueName: {#D2009PluginPath}; Flags: uninsdeletevalue deletevalue; Components: D2009

Root: HKCU; Subkey: {#D2010KnownPackageKey}; ValueType: expandsz; ValueName: {#D2010PluginPath}; ValueData: {#PluginIDEDescription}; Components: D2010; Languages: ; Flags: uninsdeletevalue
Root: HKCU; Subkey: {#D2010DisabledPackageKey}; ValueType: expandsz; ValueName: {#D2010PluginPath}; Flags: uninsdeletevalue deletevalue; Components: D2010

[Icons]
Name: {group}\{cm:UninstallProgram,{#PluginName}}; Filename: {uninstallexe}

[Code]

procedure CreateWatchFolder();
begin
  ForceDirectories(ExpandConstant('{#WatchFolder}'))
end;

procedure DeleteWatchFolder();
begin
  DelTree(ExpandConstant('{#WatchFolder}'), true, true, true);
end;

procedure DeinitializeUninstall();
begin
  DeleteWatchFolder;
end;
